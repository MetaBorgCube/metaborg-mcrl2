// Grammar of the mCRL2 language

module mcrl2/Spec

imports

  mcrl2/Common
  mcrl2/Reserved
  mcrl2/Sort
  mcrl2/Eqn
  mcrl2/Data
  mcrl2/Proc

template options

  keyword -/- [a-zA-Z\_0-9\'] // require layout after a keyword

context-free start-symbols

  MCRL2Spec

context-free syntax //--- mCRL2 specification

  MCRL2Spec.MCRL2Spec = <
    <{MCRL2SpecElt "\n"}*>
    <Init>
    <{MCRL2SpecElt "\n"}*>
  >

  MCRL2SpecElt = SortSpec
  MCRL2SpecElt = ConsSpec
  MCRL2SpecElt = MapSpec
  MCRL2SpecElt = EqnSpec
  MCRL2SpecElt = GlobVarSpec
  MCRL2SpecElt = ActSpec
  MCRL2SpecElt = ProcSpec

context-free priorities //--- Data and process conflicts

  ProcExpr.IfThen <0> .> DataExpr.Conj,
  ProcExpr.IfThen <0> .> DataExpr.Add_SetBagUnion,
  ProcExpr.IfThen <0> .> DataExpr.EltAt,

  ProcExpr.IfThenElse <0> .> DataExpr.Conj,
  ProcExpr.IfThenElse <0> .> DataExpr.Add_SetBagUnion,
  ProcExpr.IfThenElse <0> .> DataExpr.EltAt,

  ProcExpr.At <1> .> DataExpr.Conj,
  ProcExpr.At <1> .> DataExpr.Add_SetBagUnion,
  ProcExpr.At <1> .> DataExpr.EltAt

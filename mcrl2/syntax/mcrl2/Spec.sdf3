// Grammar of the mCRL2 language

module mcrl2/Spec

imports

  mcrl2/Common
  mcrl2/Reserved
  mcrl2/Sort
  mcrl2/Eqn
  mcrl2/Data
  mcrl2/Proc

template options

  keyword -/- [a-zA-Z\_0-9\'] // require layout after a keyword

context-free start-symbols

  MCRL2-Spec

context-free syntax //--- mCRL2 specification

  MCRL2-Spec.MCRL2Spec = <
    <{MCRL2-SpecElt "\n"}*>
    <MCRL2-Init>
    <{MCRL2-SpecElt "\n"}*>
  >

  MCRL2-SpecElt = MCRL2-SortSpec
  MCRL2-SpecElt = MCRL2-ConsSpec
  MCRL2-SpecElt = MCRL2-MapSpec
  MCRL2-SpecElt = MCRL2-EqnSpec
  MCRL2-SpecElt = MCRL2-GlobVarSpec
  MCRL2-SpecElt = MCRL2-ActSpec
  MCRL2-SpecElt = MCRL2-ProcSpec

context-free priorities //--- Data and process conflicts

  MCRL2-ProcExpr.IfThen <0> .> MCRL2-DataExpr.Conj,
  MCRL2-ProcExpr.IfThen <0> .> MCRL2-DataExpr.Add_SetBagUnion,
  MCRL2-ProcExpr.IfThen <0> .> MCRL2-DataExpr.EltAt,

  MCRL2-ProcExpr.IfThenElse <0> .> MCRL2-DataExpr.Conj,
  MCRL2-ProcExpr.IfThenElse <0> .> MCRL2-DataExpr.Add_SetBagUnion,
  MCRL2-ProcExpr.IfThenElse <0> .> MCRL2-DataExpr.EltAt,

  MCRL2-ProcExpr.At <1> .> MCRL2-DataExpr.Conj,
  MCRL2-ProcExpr.At <1> .> MCRL2-DataExpr.Add_SetBagUnion,
  MCRL2-ProcExpr.At <1> .> MCRL2-DataExpr.EltAt

module mcrl2/Sort //--- Sort expressions and sort declarations

imports

  mcrl2/Common

template options

  keyword -/- [a-zA-Z\_0-9\'] // require layout after a keyword

context-free syntax //--- Sort expressions and sort declarations

  MCRL2-SortExpr.Bool     = <Bool>
  MCRL2-SortExpr.Pos      = <Pos>
  MCRL2-SortExpr.Nat      = <Nat>
  MCRL2-SortExpr.Int      = <Int>
  MCRL2-SortExpr.Real     = <Real>
  syntax
  MCRL2-SortExpr-CF.List  = "List" "(" LAYOUT?-CF MCRL2-SortExpr-CF LAYOUT?-CF ")"
  MCRL2-SortExpr-CF.Set   = "Set" "(" LAYOUT?-CF MCRL2-SortExpr-CF LAYOUT?-CF ")"
  MCRL2-SortExpr-CF.Bag   = "Bag" "(" LAYOUT?-CF MCRL2-SortExpr-CF LAYOUT?-CF ")"
  MCRL2-SortExpr-CF.FSet  = "FSet" "(" LAYOUT?-CF MCRL2-SortExpr-CF LAYOUT?-CF ")"
  MCRL2-SortExpr-CF.FBag  = "FBag" "(" LAYOUT?-CF MCRL2-SortExpr-CF LAYOUT?-CF ")"
  context-free syntax
  MCRL2-SortExpr.SortRef  = MCRL2-ID
  MCRL2-SortExpr          = <(<MCRL2-SortExpr>)> {bracket}
  MCRL2-SortExpr.Struct   = <struct <{MCRL2-ConstrDecl " | "}+>>
  MCRL2-SortExpr.Function = [[MCRL2-SortExpr] -> [MCRL2-SortExpr]] {right}
  MCRL2-SortExpr.Product  = MCRL2-Product
  MCRL2-Product = <<MCRL2-SortExpr> # <{MCRL2-SortExpr " # "}+>>

  MCRL2-SortProduct = MCRL2-SortExpr

  MCRL2-SortSpec.SortSpec = <sort <{MCRL2-SortDecl " "}+>>

  MCRL2-SortDecl.SortDecl  = <<{MCRL2-ID ", "}+>;>
  MCRL2-SortDecl.SortAlias = <<MCRL2-ID> = <MCRL2-SortExpr>;>

  MCRL2-ConstrDecl.ConstrDecl = <<MCRL2-ID><MCRL2-ConstrDecl-Projs?><MCRL2-ConstrDecl-Recog?>>
  MCRL2-ConstrDecl-Projs       = <(<{MCRL2-ProjDecl ", "}+>)>
  MCRL2-ConstrDecl-Recog.Recog = <?<MCRL2-ID>>

  MCRL2-ProjDecl.ProjDecl = <<MCRL2-ProjDecl-Id?><MCRL2-SortExpr>>
  MCRL2-ProjDecl-Id = <<MCRL2-ID> : >

context-free syntax //--- Constructors and mappings

  MCRL2-IdsDecl.IdsDecl = <<{MCRL2-ID ", "}+> : <MCRL2-SortExpr>>
  
  MCRL2-ConsSpec.ConsSpec = <cons <MCRL2-ConsSpec-Decl+>>
  MCRL2-ConsSpec-Decl = <<MCRL2-IdsDecl>;>

  MCRL2-MapSpec.MapSpec = <map <MCRL2-MapSpec-Decl+>>
  MCRL2-MapSpec-Decl = <<MCRL2-IdsDecl>;>

context-free priorities

  /* 1 */ { left:
    MCRL2-SortExpr.Product
  }
  >
  /* 0 */ { right:
    MCRL2-SortExpr.Function
  }

module pp

imports

  libstratego-gpp
  libspoofax/sdf/pp
  libspoofax/editor/refactoring/-
  signatures/-
  desugar
  pp/-
  pp/mcrl2/-
  mcrl2/syntax/-

rules

  editor-format:
    (node, _, ast, path, project-path) -> (filename, result)
    with
      ext      := <get-extension> path
    ; filename := <guarantee-extension(|$[pp.[ext]])> path
    ; result   := <pp-mcrl2typecheck-string> node

rules

  pp-mcrl2typecheck-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-start-symbols)

  pp-Context-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Context)

  pp-Derivations-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Derivations)

  pp-DataExpr-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-MCRL2-DataExpr)

  pp-Type-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Type)

  pp-Scheme-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Scheme)

  pp-AllTypings-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-AllTypings)

  pp-MinimalTypings-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-MinimalTypings)

  pp-Typing-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Typing)

  pp-Subtype-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Subtype)

  pp-ConstraintStore-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-ConstraintStore)

  pp-Substitution-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-Substitution)

  pp-ConstraintGraph-string =
    pp-mcrl2typecheck-string(prettyprint-mcrl2-ConstraintGraph)

  pp-mcrl2typecheck-string(pp) =
    resugar-all
  ; parenthesize-mcrl2typecheck
  ; pp
  ; !V([], <id>)
  ; box2text-string(|120)

  pp-partial-mcrl2typecheck-string =
    resugar-all
  ; parenthesize-mcrl2typecheck
  ; prettyprint-mcrl2
  ; !V([], <id>)
  ; box2text-string(|120)

  pp-partial-mcrl2typecheck-string(|sort) =
    resugar-all
  ; parenthesize-mcrl2typecheck
  ; prettyprint-mcrl2(|sort)
  ; !V([], <id>)
  ; box2text-string(|120)

rules

  external constraint-store-to-string(pp|)

  constraint-store-to-string =
    constraint-store-to-string(pp-partial-mcrl2typecheck-string)

  prettyprint-mcrl2-ConstraintStore =
    not(is-list)
  ; ![S(<constraint-store-to-string>)]

  prettyprint-mcrl2-Int =
    ![S(<int-to-string>)]

rules

  // syntactic types

  mcrl2typecheckParenthesize :
    Function(ts, t) -> Function(ts', t)
    where [_|_] := <filter(?Function(_, _))> ts
        ; ts' := <map(try(?Function(_, _); !Parenthetical(<id>)))> ts

  mcrl2typecheckParenthesize :
    As(t, se) -> As(t', se)
    where < ?ForAll(_, _)
          + ?Exists(_, _)
          + ?Lambda(_, _)
          + ?Impl(_, _)
          + ?Conj(_, _)
          + ?Disj(_, _)
          + ?Equal(_, _)
          + ?Inequal(_, _)
          + ?Smaller(_, _)
          + ?SmallerOrEqual(_, _)
          + ?LargerOrEqual(_, _)
          + ?Larger(_, _)
          + ?Member(_, _)
          + ?ListCons(_, _)
          + ?ListSnoc(_, _)
          + ?ListConc(_, _)
          + ?Add_SetBagUnion(_, _)
          + ?Sub_SetBagDiff(_, _)
          + ?Div(_, _)
          + ?IntDiv(_, _)
          + ?IntMod(_, _)
          + ?Mul_SetBagIsect(_, _)
          + ?EltAt(_, _)
          + ?Where(_, _)
          > := t
        ; t' := Parenthetical(t)

  mcrl2typecheckParenthesize :
    FunApp(t, ts) -> FunApp(t', ts)
    where < ?ForAll(_, _)
          + ?Exists(_, _)
          + ?Lambda(_, _)
          + ?Impl(_, _)
          + ?Conj(_, _)
          + ?Disj(_, _)
          + ?Equal(_, _)
          + ?Inequal(_, _)
          + ?Smaller(_, _)
          + ?SmallerOrEqual(_, _)
          + ?LargerOrEqual(_, _)
          + ?Larger(_, _)
          + ?Member(_, _)
          + ?ListCons(_, _)
          + ?ListSnoc(_, _)
          + ?ListConc(_, _)
          + ?Add_SetBagUnion(_, _)
          + ?Sub_SetBagDiff(_, _)
          + ?Div(_, _)
          + ?IntDiv(_, _)
          + ?IntMod(_, _)
          + ?Mul_SetBagIsect(_, _)
          + ?EltAt(_, _)
          + ?Where(_, _)
          > := t
        ; t' := Parenthetical(t)

  // semantic types

  mcrl2typecheckParenthesize :
    FUNCTION(ts, t) -> FUNCTION(ts', t)
    where [_|_] := <filter(?FUNCTION(_, _))> ts
        ; ts' := <map(try(?FUNCTION(_, _); !Parenthetical(<id>)))> ts

  mcrl2typecheckParenthesize :
    FUNCTION(ts, t) -> FUNCTION(ts', t)
    where [_|_] := <filter(?PRODUCT(_))> ts
        ; ts' := <map(try(?PRODUCT(_); !Parenthetical(<id>)))> ts

  mcrl2typecheckParenthesize :
    PRODUCT(ts) -> PRODUCT(ts')
    where [_|_] := <filter(?FUNCTION(_, _))> ts
        ; ts' := <map(try(?FUNCTION(_, _); !Parenthetical(<id>)))> ts

  mcrl2typecheckParenthesize :
    PRODUCT(ts) -> PRODUCT(ts')
    where [_|_] := <filter(?PRODUCT(_))> ts
        ; ts' := <map(try(?PRODUCT(_); !Parenthetical(<id>)))> ts

rules
  
  construct-textual-change = construct-textual-change(pp-partial-mcrl2typecheck-string, parenthesize, override-reconstruction, resugar)
